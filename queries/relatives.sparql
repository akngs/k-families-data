SELECT ?human ?humanLabel ?humanDescription ?gender ?birthdate ?deathdate ?nationality ?invReltype ?relative
WHERE {
  {
    SELECT ?human ?humanDescription ?gender ?birthdate ?deathdate ?nationality ?invReltype ?relative
    WHERE {
      ?relative
                # who has nationality(wdt:P27) [located in(wdt:P706) Korean Peninsula(Q483134) ] ;
                wdt:P27 [wdt:P706 wd:Q483134 ] ;
                wdt:P569 ?birthdate ;
                # who has relation with ?human
                ?invReltype ?human .
      OPTIONAL { ?human wdt:P21 ?gender . }
      OPTIONAL { ?human wdt:P570 ?deathdate . }
      OPTIONAL { ?human wdt:P27 ?nationality . }

      # father(wdt:P22), mother(wdt:P25), spouse(wdt:P26), child(wdt:P40), sibling(wdt:P3373)
      FILTER(?invReltype in (wdt:P22,wdt:P25,wdt:P26,wdt:P40,wdt:P3373))
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "ko,en" . }
}
