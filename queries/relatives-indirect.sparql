SELECT ?human ?humanLabel ?humanDescription ?gender ?birthdate ?deathdate ?nationality ?nationalityLabel ?invReltype ?relative
WHERE {
  {
    SELECT ?human ?humanDescription ?gender ?birthdate ?deathdate ?nationality ?invReltype ?relative
    WHERE {

      ?relative
                # who has nationality(wdt:P27) [located in(wdt:P706) Korean Peninsula(Q483134) ] ;
                wdt:P27 [wdt:P706 wd:Q483134 ] ;
                # the statement has type of kinship(pq:P1039) ?invReltype .
                p:P1038 [ps:P1038 ?human ; pq:P1039 ?invReltype] ;
                # is instance of(wdt:p31) human(wd:Q5) ; // this comes after the above conditions for optimization purpose
                wdt:P31 wd:Q5 ;
                # who has relative(wdt:P1038) ?human ;
                wdt:P1038 ?human .
      OPTIONAL { ?human wdt:P27 ?nationality . }
      OPTIONAL { ?human wdt:P21 ?gender . }
      OPTIONAL { ?human wdt:P569 ?birthdate . }
      OPTIONAL { ?human wdt:P570 ?deathdate . }

      # grandmother(wd:Q9235758), grandfather(wd:Q9238344), sibling(wd:Q31184)
      FILTER(?invReltype in (wd:Q9235758, wd:Q9238344, wd:Q31184))
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "ko,en" . }
}
