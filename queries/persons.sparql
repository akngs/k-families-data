SELECT ?human ?humanLabel ?humanDescription ?gender ?birthdate ?deathdate ?nationality ?nationalityLabel
WHERE {
  {
    SELECT ?human ?humanDescription ?gender ?birthdate ?deathdate ?nationality
    WHERE {
      ?human
             # who has nationality(wdt:P27) [located in(wdt:P706) Korean Peninsula(Q483134) ] ;
             wdt:P27 [wdt:P706 wd:Q483134 ] ;
             # is instance of(wdt:p31) human(wd:Q5) ; // this comes after the above condition for optimization purpose
             wdt:P31 wd:Q5 ;
             # call it as ?nationality .
             wdt:P27 ?nationality .
      OPTIONAL { ?human wdt:P21 ?gender . }
      OPTIONAL { ?human wdt:P569 ?birthdate . }
      OPTIONAL { ?human wdt:P570 ?deathdate . }
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "ko,en" . }
}